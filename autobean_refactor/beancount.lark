file: (_NL* directive)* _NL*
?directive: option
          | include
          | plugin

// Directive labels
OPTION:       "option"
INCLUDE:      "include"
PLUGIN:       "plugin"

option:       OPTION ESCAPED_STRING ESCAPED_STRING
include:      INCLUDE ESCAPED_STRING
plugin:       PLUGIN ESCAPED_STRING [ESCAPED_STRING]

ESCAPED_STRING : /".*?(?<!\\)(\\\\)*?"/s
INDENT: /^[ \t]+(?=[^ \t\r\n])/m
WS_INLINE: /(?<!^)[ \t]+/m | /^[ \t]+(?=\r|$)/m
_NL: /\r*\n/
COMMENT_INLINE: /;[^\r\n]*$/m
COMMENT_LINE: [INDENT] COMMENT_INLINE [_NL]

%ignore WS_INLINE
%ignore COMMENT_INLINE
%ignore COMMENT_LINE

// TODO: remove once tokens are referenced elsewhere.
_unused: INDENT | ACCOUNT | NULL | BOOL | DATE | CURRENCY | TAG | LINK | POSTING_FLAG | TRANSACTION_FLAG | NUMBER

// Account
// See: https://github.com/beancount/beancount/blob/e1716b492c7619682a6d7c33c4873aa41954af1e/beancount/parser/lexer.l#L129-L130
_NON_ASCII: /[^\x00-\x7f]/
_ACCOUNT_TYPE: (/[A-Z]/ | _NON_ASCII) (/[A-Za-z0-9\-]/ | _NON_ASCII)*
_ACCOUNT_NAME: (/[A-Z0-9]/ | _NON_ASCII) (/[A-Za-z0-9\-]/ | _NON_ASCII)*
ACCOUNT: _ACCOUNT_TYPE (":" _ACCOUNT_NAME)+

// Null
// This is called "NONE" in beancount grammar but we call it NULL to avoid collision with Python None.
NULL.10: "NULL"

// Bool
BOOL.10: "TRUE" | "FALSE"

// Date
// This is more strict than beancount grammar which allows 2000-123-456
DATE: /[0-9]{4,}[-\/][0-9]{1,2}[-\/][0-9]{1,2}/

// Currency
_CURRENCY_BODY: /[A-Z0-9'._-]*/
CURRENCY: /[A-Z]/ _CURRENCY_BODY /[A-Z0-9]/
        | "/" _CURRENCY_BODY /[A-Z]/ [_CURRENCY_BODY /[A-Z0-9]/]

// Tag
TAG: /#[A-Za-z0-9-_\/.]+/

// Link
LINK: /\^[A-Za-z0-9-_\/.]+/

// Flag
// This is v2 syntax. v3 has a different syntax due to support of single-letter currency:
// (v3) FLAG: /[!&#?%]/ | /'[A-Z]/
// See: https://github.com/beancount/beancount/commit/d2d0a35e629408c9ce364eea5601839f8f582208
POSTING_FLAG: /[*!&#?%PSTCURM]/
TRANSACTION_FLAG: POSTING_FLAG | "txn"

// Number
NUMBER: (/([0-9]{1,3})(,[0-9]{3})+/ | /[0-9]+/) [/\.[0-9]*/]
