file: ((directive | EOL) _NEWLINE?)*
?directive: option
          | include
          | plugin

// Labels
OPTION:       "option"
INCLUDE:      "include"
PLUGIN:       "plugin"
PUSHTAG:      "pushtag"
POPTAG:       "poptag"
PUSHMETA:     "pushmeta"
POPMETA:      "popmeta"
BALANCE:      "balance"
CLOSE:        "close"
COMMODITY:    "commodity"
PAD:          "pad"
EVENT:        "event"
QUERY:        "query"
PRICE:        "price"
NOTE:         "note"
DOCUMENT:     "document"
OPEN:         "open"
CUSTOM:       "custom"

// Directives
option:       OPTION ESCAPED_STRING ESCAPED_STRING EOL
include:      INCLUDE ESCAPED_STRING EOL
plugin:       PLUGIN ESCAPED_STRING maybe_left{ESCAPED_STRING} EOL
pushtag:      PUSHTAG TAG EOL
poptag:       POPTAG TAG EOL
pushmeta:     PUSHMETA META_KEY maybe_left{meta_value} EOL
popmeta:      POPMETA META_KEY EOL

// Entries
balance:      DATE BALANCE ACCOUNT number_expr maybe_left{tolerance} CURRENCY EOL
close:        DATE CLOSE ACCOUNT EOL _meta_items
commodity:    DATE COMMODITY CURRENCY EOL
pad:          DATE PAD ACCOUNT ACCOUNT EOL
event:        DATE EVENT ESCAPED_STRING ESCAPED_STRING EOL
query:        DATE QUERY ESCAPED_STRING ESCAPED_STRING EOL
price:        DATE PRICE CURRENCY amount EOL
note:         DATE NOTE ACCOUNT ESCAPED_STRING EOL
document:     DATE DOCUMENT ACCOUNT ESCAPED_STRING repeated{_tag_or_link} EOL
open:         DATE OPEN ACCOUNT repeated_sep{CURRENCY, _COMMA} maybe_left{ESCAPED_STRING} EOL
custom:       DATE CUSTOM ESCAPED_STRING repeated{_custom_value} EOL

// Punctuations
ESCAPED_STRING : /".*?(?<!\\)(\\\\)*?"/s
INDENT: /^[ \t]+(?=[^ \t\r\n])/m
WS_INLINE: /(?<!^)[ \t]+/m | /^[ \t]+(?=\r|$)/m
_NEWLINE: /\r*\n/
COMMENT_INLINE: /;[^\r\n]*$/m
// indented cmoment line may appear anywhere but unindented ones cannot appear inside a indented block
COMMENT_LINE: /^/m INDENT COMMENT_INLINE _NEWLINE?
_COMMA: ","

%declare EOL
%ignore WS_INLINE
%ignore COMMENT_INLINE
%ignore COMMENT_LINE

// TODO: remove once tokens are referenced elsewhere.
_unused: TRANSACTION_FLAG

// Account
// See: https://github.com/beancount/beancount/blob/e1716b492c7619682a6d7c33c4873aa41954af1e/beancount/parser/lexer.l#L129-L130
_NON_ASCII: /[^\x00-\x7f]/
_ACCOUNT_TYPE: (/[A-Z]/ | _NON_ASCII) (/[A-Za-z0-9\-]/ | _NON_ASCII)*
_ACCOUNT_NAME: (/[A-Z0-9]/ | _NON_ASCII) (/[A-Za-z0-9\-]/ | _NON_ASCII)*
ACCOUNT: _ACCOUNT_TYPE (":" _ACCOUNT_NAME)+

// Null
// This is called "NONE" in beancount grammar but we call it NULL to avoid collision with Python None.
// Takes precedence over CURRENCY.
NULL.10: "NULL"

// Bool
// Takes precedence over CURRENCY.
BOOL.10: "TRUE" | "FALSE"

// Date
// This is more strict than beancount grammar which allows 2000-123-456
// Takes precedence over NUMBER.
DATE.10: /[0-9]{4,}[-\/][0-9]{1,2}[-\/][0-9]{1,2}/

// Currency
_CURRENCY_BODY: /[A-Z0-9'._-]*/
CURRENCY: /[A-Z]/ _CURRENCY_BODY /[A-Z0-9]/
        | "/" _CURRENCY_BODY /[A-Z]/ [_CURRENCY_BODY /[A-Z0-9]/]

// Tag
TAG: /#[A-Za-z0-9-_\/.]+/

// Link
LINK: /\^[A-Za-z0-9-_\/.]+/

// MetaKey
// The beancount syntax matches the trailing colon with lookahead. We make it part of the key token itself to simplify
// testing and avoid whitespaces being inserted before the colon.
META_KEY: /[a-z][a-zA-Z0-9-_]+:/

// Flag
// This is v2 syntax. v3 has a different syntax due to support of single-letter currency:
// (v3) FLAG: /[!&#?%]/ | /'[A-Z]/
// See: https://github.com/beancount/beancount/commit/d2d0a35e629408c9ce364eea5601839f8f582208
POSTING_FLAG: /[*!&#?%PSTCURM]/
TRANSACTION_FLAG: POSTING_FLAG | "txn"

// Number
NUMBER: (/([0-9]{1,3})(,[0-9]{3})+/ | /[0-9]+/) [/\.[0-9]*/]

// Arithmetic operators
UNARY_OP:     "+" | "-"
ADD_OP:       "+" | "-"
MUL_OP:       "*" | "/"
LEFT_PAREN:   "("
RIGHT_PAREN:  ")"

// Arithmetic expression
number_expr: number_add_expr
number_add_expr: number_mul_expr (ADD_OP number_mul_expr)*
number_mul_expr: number_atom_expr (MUL_OP number_atom_expr)*
?number_atom_expr: NUMBER | number_paren_expr | number_unary_expr
number_paren_expr: LEFT_PAREN number_add_expr RIGHT_PAREN
number_unary_expr: UNARY_OP number_atom_expr

// Amount
amount: number_expr CURRENCY

// MetaValue
?meta_value: ESCAPED_STRING | ACCOUNT | DATE | CURRENCY | TAG | BOOL | NULL | number_expr | amount

// Tolerance
TILDE:        "~"
tolerance:    TILDE number_expr

// tags / links
_tag_or_link: TAG | LINK

// custom value
_custom_value: ESCAPED_STRING | DATE | BOOL | amount | number_expr | ACCOUNT

// posting
_partial_amount: maybe_left{number_expr} maybe_left{CURRENCY}
AT:                "@"
ATAT:              "@@"
unit_price: AT _partial_amount
total_price: ATAT _partial_amount
?price_annotation: unit_price | total_price
LEFT_BRACE:        "{"
RIGHT_BRACE:       "}"
DBL_LEFT_BRACE:    "{{"
DBL_RIGHT_BRACE:   "}}"
HASH:              "#"
ASTERISK:          "*"
compound_amount: maybe_right{number_expr} HASH maybe_left{number_expr} CURRENCY
?cost_component: DATE | ASTERISK | ESCAPED_STRING | CURRENCY | number_expr | amount | compound_amount
unit_cost: LEFT_BRACE repeated_sep{cost_component, _COMMA} RIGHT_BRACE
total_cost: DBL_LEFT_BRACE repeated_sep{cost_component, _COMMA} DBL_RIGHT_BRACE
cost_spec: unit_cost | total_cost
posting: INDENT maybe_right{POSTING_FLAG} ACCOUNT _partial_amount maybe_left{cost_spec} maybe_left{price_annotation} EOL

// meta
meta_item: INDENT META_KEY maybe_left{meta_value} EOL
_meta_items: repeated_pre{meta_item, _NEWLINE}

// [internal] Optional field wrapper
maybe_left{inner}: [inner]
maybe_right{inner}: [inner]

// [internal] Repeated field wrapper
repeated{inner}: (inner)*
repeated_pre{inner, pre}: (pre inner)*
repeated_sep{inner, sep}: inner? (sep inner)*
