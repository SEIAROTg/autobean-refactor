file: (_NL* directive)* _NL*
?directive: option
          | include
          | plugin

// Labels
OPTION:       "option"
INCLUDE:      "include"
PLUGIN:       "plugin"
PUSHTAG:      "pushtag"
POPTAG:       "poptag"
PUSHMETA:     "pushmeta"
POPMETA:      "popmeta"
BALANCE:      "balance"
CLOSE:        "close"
COMMODITY:    "commodity"
PAD:          "pad"
EVENT:        "event"
QUERY:        "query"
PRICE:        "price"
NOTE:         "note"
DOCUMENT:     "document"
OPEN:         "open"
CUSTOM:       "custom"

// Directives
option:       OPTION ESCAPED_STRING ESCAPED_STRING
include:      INCLUDE ESCAPED_STRING
plugin:       PLUGIN ESCAPED_STRING maybe_left{ESCAPED_STRING}
pushtag:      PUSHTAG TAG
poptag:       POPTAG TAG
pushmeta:     PUSHMETA META_KEY maybe_left{meta_value}
popmeta:      POPMETA META_KEY

// Entries
balance:      DATE BALANCE ACCOUNT number_expr maybe_left{tolerance} CURRENCY
close:        DATE CLOSE ACCOUNT
commodity:    DATE COMMODITY CURRENCY
pad:          DATE PAD ACCOUNT ACCOUNT
event:        DATE EVENT ESCAPED_STRING ESCAPED_STRING
query:        DATE QUERY ESCAPED_STRING ESCAPED_STRING
price:        DATE PRICE CURRENCY amount
note:         DATE NOTE ACCOUNT ESCAPED_STRING
document:     DATE DOCUMENT ACCOUNT ESCAPED_STRING repeated{_tag_or_link}
open:         DATE OPEN ACCOUNT repeated_sep{CURRENCY, _COMMA} maybe_left{ESCAPED_STRING}
custom:       DATE CUSTOM ESCAPED_STRING repeated{_custom_value}

// Punctuations
ESCAPED_STRING : /".*?(?<!\\)(\\\\)*?"/s
INDENT: /^[ \t]+(?=[^ \t\r\n])/m
WS_INLINE: /(?<!^)[ \t]+/m | /^[ \t]+(?=\r|$)/m
_NL: /\r*\n/
COMMENT_INLINE: /;[^\r\n]*$/m
COMMENT_LINE: [INDENT] COMMENT_INLINE [_NL]
_COMMA: ","

%ignore WS_INLINE
%ignore COMMENT_INLINE
%ignore COMMENT_LINE

// TODO: remove once tokens are referenced elsewhere.
_unused: INDENT | LINK | POSTING_FLAG | TRANSACTION_FLAG

// Account
// See: https://github.com/beancount/beancount/blob/e1716b492c7619682a6d7c33c4873aa41954af1e/beancount/parser/lexer.l#L129-L130
_NON_ASCII: /[^\x00-\x7f]/
_ACCOUNT_TYPE: (/[A-Z]/ | _NON_ASCII) (/[A-Za-z0-9\-]/ | _NON_ASCII)*
_ACCOUNT_NAME: (/[A-Z0-9]/ | _NON_ASCII) (/[A-Za-z0-9\-]/ | _NON_ASCII)*
ACCOUNT: _ACCOUNT_TYPE (":" _ACCOUNT_NAME)+

// Null
// This is called "NONE" in beancount grammar but we call it NULL to avoid collision with Python None.
// Takes precedence over CURRENCY.
NULL.10: "NULL"

// Bool
// Takes precedence over CURRENCY.
BOOL.10: "TRUE" | "FALSE"

// Date
// This is more strict than beancount grammar which allows 2000-123-456
// Takes precedence over NUMBER.
DATE.10: /[0-9]{4,}[-\/][0-9]{1,2}[-\/][0-9]{1,2}/

// Currency
_CURRENCY_BODY: /[A-Z0-9'._-]*/
CURRENCY: /[A-Z]/ _CURRENCY_BODY /[A-Z0-9]/
        | "/" _CURRENCY_BODY /[A-Z]/ [_CURRENCY_BODY /[A-Z0-9]/]

// Tag
TAG: /#[A-Za-z0-9-_\/.]+/

// Link
LINK: /\^[A-Za-z0-9-_\/.]+/

// MetaKey
// The beancount syntax matches the trailing colon with lookahead. We make it part of the key token itself to simplify
// testing and avoid whitespaces being inserted before the colon.
META_KEY: /[a-z][a-zA-Z0-9-_]+:/

// Flag
// This is v2 syntax. v3 has a different syntax due to support of single-letter currency:
// (v3) FLAG: /[!&#?%]/ | /'[A-Z]/
// See: https://github.com/beancount/beancount/commit/d2d0a35e629408c9ce364eea5601839f8f582208
POSTING_FLAG: /[*!&#?%PSTCURM]/
TRANSACTION_FLAG: POSTING_FLAG | "txn"

// Number
NUMBER: (/([0-9]{1,3})(,[0-9]{3})+/ | /[0-9]+/) [/\.[0-9]*/]

// Arithmetic operators
UNARY_OP:     "+" | "-"
ADD_OP:       "+" | "-"
MUL_OP:       "*" | "/"
LEFT_PAREN:   "("
RIGHT_PAREN:  ")"

// Arithmetic expression
number_expr: number_add_expr
number_add_expr: number_mul_expr (ADD_OP number_mul_expr)*
number_mul_expr: number_atom_expr (MUL_OP number_atom_expr)*
?number_atom_expr: NUMBER | number_paren_expr | number_unary_expr
number_paren_expr: LEFT_PAREN number_add_expr RIGHT_PAREN
number_unary_expr: UNARY_OP number_atom_expr

// Amount
amount: number_expr CURRENCY

// MetaValue
?meta_value: ESCAPED_STRING | ACCOUNT | DATE | CURRENCY | TAG | BOOL | NULL | number_expr | amount

// Tolerance
TILDE:        "~"
tolerance:    TILDE number_expr

// tags / links
_tag_or_link: TAG | LINK

// custom value
_custom_value: ESCAPED_STRING | DATE | BOOL | amount | number_expr | ACCOUNT

// posting
_partial_amount: maybe_left{number_expr} maybe_left{CURRENCY}
AT:                "@"
ATAT:              "@@"
unit_price: AT _partial_amount
total_price: ATAT _partial_amount
_price_annotation: unit_price | total_price

// [internal] Optional field wrapper
maybe_left{inner}: [inner]
maybe_right{inner}: [inner]

// [internal] Repeated field wrapper
repeated{inner}: (inner)*
repeated_sep{inner, sep}: inner? (sep inner)*
