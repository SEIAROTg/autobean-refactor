file: repeated{_file_directive}
?directive: option
          | include
          | plugin
          | pushtag
          | poptag
          | poptag
          | pushmeta
          | popmeta
          | balance
          | close
          | commodity
          | pad
          | event
          | query
          | price
          | note
          | document
          | open
          | custom
          | transaction
// TODO: avoid removing ignored parts when removing a directive.
file_ignored_: EOL | _INDENT EOL* _DEDENT
_file_directive: directive | file_ignored_

// Labels
OPTION:       "option"
INCLUDE:      "include"
PLUGIN:       "plugin"
PUSHTAG:      "pushtag"
POPTAG:       "poptag"
PUSHMETA:     "pushmeta"
POPMETA:      "popmeta"
BALANCE:      "balance"
CLOSE:        "close"
COMMODITY:    "commodity"
PAD:          "pad"
EVENT:        "event"
QUERY:        "query"
PRICE:        "price"
NOTE:         "note"
DOCUMENT:     "document"
OPEN:         "open"
CUSTOM:       "custom"

// Directives
option:       OPTION ESCAPED_STRING ESCAPED_STRING EOL
include:      INCLUDE ESCAPED_STRING EOL
plugin:       PLUGIN ESCAPED_STRING maybe_left{ESCAPED_STRING} EOL
pushtag:      PUSHTAG TAG EOL
poptag:       POPTAG TAG EOL
pushmeta:     PUSHMETA META_KEY maybe_left{meta_value} EOL
popmeta:      POPMETA META_KEY EOL

// Entries
balance:      DATE BALANCE ACCOUNT number_expr maybe_left{tolerance} CURRENCY EOL _meta_items
close:        DATE CLOSE ACCOUNT EOL _meta_items
commodity:    DATE COMMODITY CURRENCY EOL _meta_items
pad:          DATE PAD ACCOUNT ACCOUNT EOL _meta_items
event:        DATE EVENT ESCAPED_STRING ESCAPED_STRING EOL _meta_items
query:        DATE QUERY ESCAPED_STRING ESCAPED_STRING EOL _meta_items
price:        DATE PRICE CURRENCY amount EOL _meta_items
note:         DATE NOTE ACCOUNT ESCAPED_STRING EOL _meta_items
document:     DATE DOCUMENT ACCOUNT ESCAPED_STRING repeated{_tag_or_link} EOL _meta_items
open:         DATE OPEN ACCOUNT repeated_sep{CURRENCY, _COMMA} maybe_left{ESCAPED_STRING} EOL _meta_items
custom:       DATE CUSTOM ESCAPED_STRING repeated{_custom_value} EOL _meta_items
transaction:  DATE TRANSACTION_FLAG  maybe_left{NEVER} maybe_left{ESCAPED_STRING} maybe_left{ESCAPED_STRING} repeated{_tag_or_link} EOL _meta_items_or_postings
_meta_items_or_postings: _INDENT repeated{meta_item} repeated{posting} _DEDENT | repeated{NEVER} repeated{NEVER}

// Punctuations
WHITESPACE: /[ \t]+/
_NEWLINE: /\r*\n/
// Takes precedence over INLINE_COMMENT.
_NEWLINE_INDENT_COMMENT.10: (_NEWLINE | /\A/) (BLOCK_COMMENT | WHITESPACE) | _NEWLINE
INLINE_COMMENT: /;[^\r\n]*/s
BLOCK_COMMENT: /^/m INLINE_COMMENT (_NEWLINE INLINE_COMMENT)* | /^/m WHITESPACE INLINE_COMMENT (_NEWLINE WHITESPACE INLINE_COMMENT)*
_COMMA: ","

%declare EOL
%declare _INDENT
%declare _DEDENT
%ignore WHITESPACE
%ignore _NEWLINE
%ignore INLINE_COMMENT
%ignore BLOCK_COMMENT

// String
ESCAPED_STRING : /".*?(?<!\\)(\\\\)*?"/s

// Account
// See: https://github.com/beancount/beancount/blob/e1716b492c7619682a6d7c33c4873aa41954af1e/beancount/parser/lexer.l#L129-L130
_NON_ASCII: /[^\x00-\x7f]/
_ACCOUNT_TYPE: (/[A-Z]/ | _NON_ASCII) (/[A-Za-z0-9\-]/ | _NON_ASCII)*
_ACCOUNT_NAME: (/[A-Z0-9]/ | _NON_ASCII) (/[A-Za-z0-9\-]/ | _NON_ASCII)*
ACCOUNT: _ACCOUNT_TYPE (":" _ACCOUNT_NAME)+

// Null
// This is called "NONE" in beancount grammar but we call it NULL to avoid collision with Python None.
// Takes precedence over CURRENCY.
NULL.10: "NULL"

// Bool
// Takes precedence over CURRENCY.
BOOL.10: "TRUE" | "FALSE"

// Date
// This is more strict than beancount grammar which allows 2000-123-456
// Takes precedence over NUMBER.
DATE.10: /[0-9]{4,}[-\/][0-9]{1,2}[-\/][0-9]{1,2}/

// Currency
_CURRENCY_BODY: /[A-Z0-9'._-]*/
CURRENCY: /[A-Z]/ _CURRENCY_BODY /[A-Z0-9]/
        | "/" _CURRENCY_BODY /[A-Z]/ [_CURRENCY_BODY /[A-Z0-9]/]

// Tag
TAG: /#[A-Za-z0-9-_\/.]+/

// Link
LINK: /\^[A-Za-z0-9-_\/.]+/

// Flag
// This is v2 syntax. v3 has a different syntax due to support of single-letter currency:
// (v3) FLAG: /[!&#?%]/ | /'[A-Z]/
// See: https://github.com/beancount/beancount/commit/d2d0a35e629408c9ce364eea5601839f8f582208
POSTING_FLAG: /[*!&#?%PSTCURM]/
TRANSACTION_FLAG: POSTING_FLAG | "txn"

// Number
NUMBER: (/([0-9]{1,3})(,[0-9]{3})+/ | /[0-9]+/) [/\.[0-9]*/]

// Arithmetic operators
UNARY_OP:     "+" | "-"
ADD_OP:       "+" | "-"
MUL_OP:       "*" | "/"
LEFT_PAREN:   "("
RIGHT_PAREN:  ")"

// Arithmetic expression
number_expr: number_add_expr
number_add_expr: number_mul_expr (ADD_OP number_mul_expr)*
number_mul_expr: number_atom_expr (MUL_OP number_atom_expr)*
?number_atom_expr: NUMBER | number_paren_expr | number_unary_expr
number_paren_expr: LEFT_PAREN number_add_expr RIGHT_PAREN
number_unary_expr: UNARY_OP number_atom_expr

// Amount
amount: number_expr CURRENCY

// MetaKey
// The beancount syntax matches the trailing colon with lookahead. We make it part of the key token itself to simplify
// testing and avoid whitespaces being inserted before the colon.
META_KEY: /[a-z][a-zA-Z0-9-_]+:/

// MetaValue
?meta_value: ESCAPED_STRING | ACCOUNT | DATE | CURRENCY | TAG | BOOL | NULL | number_expr | amount

// MetaItem
_meta_item: META_KEY maybe_left{meta_value} EOL
meta_item: _allow_direct_parse{_meta_item}
comment_line_: BLOCK_COMMENT EOL
_meta_items_entity: meta_item | comment_line_
_meta_items: _INDENT repeated{_meta_items_entity} _DEDENT | repeated{NEVER}

// Tolerance
TILDE:        "~"
tolerance:    TILDE number_expr

// tags / links
_tag_or_link: TAG | LINK

// custom value
_custom_value: ESCAPED_STRING | DATE | BOOL | amount | number_expr | ACCOUNT

// posting
_partial_amount: maybe_left{number_expr} maybe_left{CURRENCY}
AT:                "@"
ATAT:              "@@"
unit_price: AT _partial_amount
total_price: ATAT _partial_amount
?price_annotation: unit_price | total_price
LEFT_BRACE:        "{"
RIGHT_BRACE:       "}"
DBL_LEFT_BRACE:    "{{"
DBL_RIGHT_BRACE:   "}}"
HASH:              "#"
ASTERISK:          "*"
compound_amount: maybe_right{number_expr} HASH maybe_left{number_expr} CURRENCY
?cost_component: DATE | ASTERISK | ESCAPED_STRING | CURRENCY | number_expr | amount | compound_amount
unit_cost: LEFT_BRACE repeated_sep{cost_component, _COMMA} RIGHT_BRACE
total_cost: DBL_LEFT_BRACE repeated_sep{cost_component, _COMMA} DBL_RIGHT_BRACE
cost_spec: unit_cost | total_cost
_posting: maybe_right{POSTING_FLAG} ACCOUNT _partial_amount maybe_left{cost_spec} maybe_left{price_annotation} EOL repeated{meta_item}
posting: _allow_direct_parse{_posting}

// [internal] Optional field wrapper
maybe_left{inner}: [inner]
maybe_left_always{inner}: inner
maybe_right{inner}: [inner]

// [internal] Repeated field wrapper
repeated{inner}: (inner)*
repeated_sep{inner, sep}: inner? (sep inner)*

// [internal]
NEVER: /^_^/
_allow_direct_parse{inner}: inner | _INDENT inner _DEDENT  // allow parse without parent model
